import java.awt.Desktop

plugins {
	// basic build plugins
	id "java"
	id "war"
	id "org.gretty" version "3.0.8"

	// code formatting
	id "com.diffplug.spotless" version "6.8.0"

	// code coverage
	id 'jacoco'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'javax.servlet:javax.servlet-api:3.1.0'

	// https://mvnrepository.com/artifact/com.google.code.gson/gson
	implementation 'com.google.code.gson:gson:2.8.6'

	// https://mvnrepository.com/artifact/org.json/json
	implementation 'org.json:json:20200518'

	// https://mvnrepository.com/artifact/com.google.googlejavaformat/google-java-format
	implementation 'com.google.googlejavaformat:google-java-format:1.7'

	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation 'org.postgresql:postgresql:42.4.0'
	implementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	finalizedBy jacocoTestCoverageVerification
}

jacocoTestReport {
	dependsOn test
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.0
			}
		}
	}
}

task openJacoco {
	doLast {
		Desktop.desktop.open(new File("build/reports/jacoco/test/html/index.html"))
	}
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		target 'src/*/java/**/*.java'
		// apply a specific flavor of google-java-format
		googleJavaFormat('1.7').aosp()
	}
}
